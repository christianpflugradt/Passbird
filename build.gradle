plugins {
    id 'java'
    id 'java-library'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'com.github.hierynomus.license' version '0.16.1'
    id 'com.github.spotbugs' version '4.7.6'
    id 'org.owasp.dependencycheck' version '6.3.2'
    id 'name.remal.check-dependency-updates' version '1.5.0'
}

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
    jcenter()
}

pmd {
    consoleOutput = true
    maxFailures = 0
    ruleSets = []
    ruleSetConfig = resources.text.fromFile(rootProject.file("config/pmd/ruleset.xml"))
}

checkstyle {
    maxWarnings = 0
    maxErrors = 0
    configFile = project(':').file('config/checkstyle/checkstyle.xml')
    configProperties = ["checkstyle.suppressions.file" : project(':').file('config/checkstyle/suppressions.xml')]
    toolVersion = '8.32'
}

spotbugs {
    showProgress = true
    effort = "max"
    reportLevel = "low"
    excludeFilter = rootProject.file("config/spotbugs/excludes.xml")
}

spotbugsMain {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

jacocoTestReport {
    executionData.from = files('build/jacoco/unittests.exec', 'build/jacoco/integrationtests.exec')
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

jacocoTestCoverageVerification {
    violationRules {[
        rule {
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.95
            }
            failOnViolation true
        },
        rule {
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.90
            }
            failOnViolation true
        }]
    }
}

dependencyCheck {
    failBuildOnCVSS = 3
    suppressionFile = 'config/owasp/suppressions.xml'
}

task architecturetests(type: Test, group: 'Verification') {
    useJUnitPlatform()
    include '**/*TestAT.class'
}

task unittests(type: Test, group: 'Verification') {
    useJUnitPlatform()
    include '**/*Test.class'
    testLogging {
        events "passed", "failed"
    }
}

task integrationtests(type: Test, group: 'Verification') {
    useJUnitPlatform()
    include '**/*TestIT.class'
    testLogging {
        events "passed", "failed"
    }
}

task checkAll() {
    dependsOn checkstyleMain
    dependsOn pmdMain
    dependsOn spotbugsMain
    dependsOn unittests
    dependsOn integrationtests
    dependsOn architecturetests
    dependsOn jacocoTestCoverageVerification
    dependsOn jacocoTestReport
    dependsOn tasks.findByName('downloadLicenses')
    tasks.findByName('jacocoTestReport').mustRunAfter 'unittests'
    tasks.findByName('jacocoTestReport').mustRunAfter 'integrationtests'
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.0'
    implementation 'io.vavr:vavr:0.10.4'
    implementation 'com.google.inject:guice:5.0.1'
    implementation 'com.google.inject.extensions:guice-assistedinject:5.0.1'
    implementation 'com.google.guava:guava:31.0.1-jre'
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
    testImplementation  'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation  'org.mockito:mockito-junit-jupiter:3.12.4'
    testImplementation  'org.mockito:mockito-core:3.12.4'
    testImplementation  'org.assertj:assertj-core:3.21.0'
    testImplementation  'org.awaitility:awaitility:4.1.0'
    testImplementation  'com.tngtech.archunit:archunit:0.15.0'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes "Main-Class": "de.pflugradts.pwman3.application.Main",
       'Implementation-Version': project.version && !project.version.empty ? project.version : 'snapshot'
    }

    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
